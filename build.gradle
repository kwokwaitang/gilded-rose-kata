plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

    // Cucumber
    implementation 'org.codehaus.groovy:groovy:3.0.8'
    testImplementation 'io.cucumber:cucumber-java:6.10.4'
    testImplementation 'io.cucumber:cucumber-junit:6.10.4'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // https://mvnrepository.com/artifact/org.hamcrest/hamcrest
    testImplementation 'org.hamcrest:hamcrest:2.2'

    // https://mvnrepository.com/artifact/com.oneeyedmen/okeydoke
    implementation 'com.oneeyedmen:okeydoke:1.3.3'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

group = 'com.gildedrose'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task textTest(type: JavaExec) {
    main = "com.gildedrose.TextTestFixture"
    classpath = sourceSets.test.runtimeClasspath
    args "3"
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                '--plugin', 'pretty',
                '--plugin', 'html:target/cucumber-report.html',
                '--glue', 'com.gildedrose',
                'src/test/resources']
        }
    }
}
